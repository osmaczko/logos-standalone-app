{ pkgs, src, logosSdk, logosProtocolPkg, logosQtSdk, logosLiblogos, logosDesignSystem, logosViewModuleRuntime, logosQtMcp ? null, capabilityModuleLgx, enableInspector ? (logosQtMcp != null) }:

  pkgs.stdenv.mkDerivation rec {
    pname = "logos-standalone-app";
    version = "1.0.0";

    inherit src;

    nativeBuildInputs = [
      pkgs.cmake
      pkgs.ninja
      pkgs.pkg-config
      pkgs.qt6.wrapQtAppsHook
      logosSdk
      pkgs.patchelf
      pkgs.removeReferencesTo
      pkgs.python3
    ];

    buildInputs = [
      pkgs.qt6.qtbase
      pkgs.qt6.qtremoteobjects
      pkgs.qt6.qtdeclarative
      pkgs.qt6.qtwebview
      pkgs.qt6.qtwebsockets
      pkgs.qt6.qtsvg
      pkgs.zstd
      pkgs.krb5
      pkgs.abseil-cpp
      pkgs.zlib
      pkgs.icu
      # Qt split: the app links logos-qt-sdk::logos_qt_sdk, which carries the
      # logos-protocol link interface (OpenSSL, Boost::system, nlohmann_json).
      logosProtocolPkg
      logosQtSdk
      logosDesignSystem
    ] ++ pkgs.lib.optionals pkgs.stdenv.isLinux [
      (pkgs.webkitgtk_4_1 or pkgs.webkitgtk_4_0 or pkgs.webkitgtk)
    ];

    qtLibPath = pkgs.lib.makeLibraryPath ([
      pkgs.qt6.qtbase
      pkgs.qt6.qtremoteobjects
      pkgs.qt6.qtdeclarative
      pkgs.qt6.qtwebview
      pkgs.zstd
      pkgs.krb5
      pkgs.zlib
      pkgs.glib
      pkgs.stdenv.cc.cc
      pkgs.freetype
      pkgs.fontconfig
      # Qt split: the app binary links logos-qt-sdk → logos-protocol, whose
      # shared runtime deps (Boost.System, OpenSSL) must be reachable now
      # that the binary's RPATH is stripped for bundling.
      pkgs.boost
      pkgs.openssl
    ] ++ pkgs.lib.optionals pkgs.stdenv.isLinux [
      pkgs.libglvnd
      pkgs.mesa.drivers
      pkgs.xorg.libX11
      pkgs.xorg.libXext
      pkgs.xorg.libXrender
      pkgs.xorg.libXrandr
      pkgs.xorg.libXcursor
      pkgs.xorg.libXi
      pkgs.xorg.libXfixes
      pkgs.xorg.libxcb
    ]);

    # qtsvg carries the SVG image plugin: the design system ships its icon set as
    # SVG, and without the plugin every icon in a plugin's UI renders empty.
    qtPluginPath = "${pkgs.qt6.qtbase}/lib/qt-6/plugins:${pkgs.qt6.qtwebview}/lib/qt-6/plugins:${pkgs.qt6.qtsvg}/lib/qt-6/plugins";
    qmlImportPath = "${placeholder "out"}/lib:${pkgs.qt6.qtdeclarative}/lib/qt-6/qml:${pkgs.qt6.qtwebview}/lib/qt-6/qml";

    dontStrip = true;

    qtWrapperArgs = [
      "--prefix" "LD_LIBRARY_PATH" ":" qtLibPath
      "--prefix" "QT_PLUGIN_PATH" ":" qtPluginPath
      "--prefix" "QML_IMPORT_PATH" ":" qmlImportPath
      "--prefix" "QML2_IMPORT_PATH" ":" qmlImportPath
    ];

    preConfigure = ''
      export MACOSX_DEPLOYMENT_TARGET=12.0

      ${pkgs.lib.optionalString (enableInspector && logosQtMcp != null) ''
        echo "Copying logos-qt-mcp source for inspector..."
        mkdir -p ./logos-qt-mcp
        cp -r ${logosQtMcp}/* ./logos-qt-mcp/
      ''}

      # Note: we deliberately do NOT stage a partial cpp-sdk copy
      # under ./logos-cpp-sdk/ any more. The CMakeLists now uses
      # `find_package(logos-cpp-sdk CONFIG PATHS
      # $LOGOS_CPP_SDK_ROOT/lib/cmake/logos-cpp-sdk)`, which carries
      # include dirs + the link interface (OpenSSL, Boost,
      # nlohmann_json) via the imported target. Pointing
      # LOGOS_CPP_SDK_ROOT directly at the SDK store path (set in
      # configurePhase below) means the *Config.cmake files are
      # present where find_package looks.
    '';

    preFixup = ''
      find $out -type f -executable -exec sh -c '
        if file "$1" | grep -q "ELF.*executable"; then
          if patchelf --print-rpath "$1" 2>/dev/null | grep -q "/build/"; then
            patchelf --remove-rpath "$1" 2>/dev/null || true
          fi
          if echo "$1" | grep -q "/logos-standalone-app$"; then
            patchelf --set-rpath "$out/lib" "$1" 2>/dev/null || true
          fi
        fi
      ' _ {} \;
      find $out -name "*.so" -exec sh -c '
        if patchelf --print-rpath "$1" 2>/dev/null | grep -q "/build/"; then
          patchelf --remove-rpath "$1" 2>/dev/null || true
        fi
      ' _ {} \;
    '';

    configurePhase = ''
      runHook preConfigure

      cmake -S app -B build \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_OSX_DEPLOYMENT_TARGET=12.0 \
        -DCMAKE_INSTALL_RPATH_USE_LINK_PATH=FALSE \
        -DCMAKE_INSTALL_RPATH="" \
        -DCMAKE_SKIP_BUILD_RPATH=TRUE \
        -DLOGOS_LIBLOGOS_ROOT=${logosLiblogos} \
        -DLOGOS_CPP_SDK_ROOT=${logosSdk} \
        -DLOGOS_QT_SDK_ROOT=${logosQtSdk} \
        -DLOGOS_PROTOCOL_ROOT=${logosProtocolPkg} \
        -DLOGOS_VIEW_MODULE_RUNTIME_ROOT=${logosViewModuleRuntime} \
        -DLogosDesignSystem_DIR=${logosDesignSystem}/lib/cmake/LogosDesignSystem \
        -DENABLE_QML_INSPECTOR=${if enableInspector then "ON" else "OFF"} \
        ${pkgs.lib.optionalString (enableInspector && logosQtMcp != null) "-DLOGOS_QT_MCP_ROOT=$(pwd)/logos-qt-mcp"}

      runHook postConfigure
    '';

    buildPhase = ''
      runHook preBuild
      cmake --build build
      runHook postBuild
    '';

    installPhase = ''
      runHook preInstall

      mkdir -p $out/bin $out/lib

      cp build/bin/logos-standalone-app "$out/bin/.logos-standalone-app-bin"

      # wrapQtAppsHook does not create shell wrappers on macOS, so we do it manually
      # to ensure QML_IMPORT_PATH is set before the QML engine initialises.
      cat > "$out/bin/logos-standalone-app" << EOF
#!/bin/sh
export QML_IMPORT_PATH="$out/lib:${pkgs.qt6.qtdeclarative}/lib/qt-6/qml:${pkgs.qt6.qtwebview}/lib/qt-6/qml\''${QML_IMPORT_PATH:+:\$QML_IMPORT_PATH}"
export QML2_IMPORT_PATH="\$QML_IMPORT_PATH"
exec "$out/bin/.logos-standalone-app-bin" "\$@"
EOF
      chmod +x "$out/bin/logos-standalone-app"

      [ -f "${logosLiblogos}/bin/logos_host" ] && cp -L "${logosLiblogos}/bin/logos_host" "$out/bin/"

      # Ship ui-host binary from logos-view-module-runtime for view module support
      if [ -f "${logosViewModuleRuntime}/bin/ui-host" ]; then
        cp "${logosViewModuleRuntime}/bin/ui-host" "$out/bin/"
        echo "Installed ui-host binary from logos-view-module-runtime"
      fi

      for f in "${logosLiblogos}"/lib/*; do
        [ -f "$f" ] && cp -L "$f" "$out/lib/" || true
      done
      ls "${logosSdk}/lib/"liblogos_sdk.* >/dev/null 2>&1 && \
        cp -L "${logosSdk}/lib/"liblogos_sdk.* "$out/lib/" || true

      # Copy any shared libraries from logos-view-module-runtime so the
      # packaged app stays self-contained if the runtime is ever built as a
      # shared lib. Static (.a) builds are effectively a no-op.
      if [ -d "${logosViewModuleRuntime}/lib" ]; then
        for f in "${logosViewModuleRuntime}"/lib/liblogos_view_module_runtime.*; do
          [ -f "$f" ] && cp -L "$f" "$out/lib/" || true
        done
      fi

      # Install capability_module from its .lgx package into the directory structure
      # that logos_core expects: modules/<name>/manifest.json + <library>
      mkdir -p $out/modules
      lgx_file=$(find ${capabilityModuleLgx} -name '*.lgx' | head -1)
      if [ -n "$lgx_file" ]; then
        extract_dir=$(mktemp -d)
        tar -xzf "$lgx_file" -C "$extract_dir"

        # Find the platform variant directory
        variant_dir=""
        for v in darwin-arm64-dev darwin-amd64-dev linux-amd64-dev linux-arm64-dev \
                 darwin-arm64 darwin-amd64 linux-amd64 linux-arm64; do
          if [ -d "$extract_dir/variants/$v" ]; then
            variant_dir="$extract_dir/variants/$v"
            break
          fi
        done

        if [ -n "$variant_dir" ]; then
          # Read the module name from the root manifest
          module_name=$(python3 -c "
import json; f=open('$extract_dir/manifest.json'); print(json.load(f).get('name',str())); f.close()
")
          if [ -n "$module_name" ]; then
            mkdir -p "$out/modules/$module_name"
            # Copy the root manifest (has platform-variant main entries) and variant contents
            cp "$extract_dir/manifest.json" "$out/modules/$module_name/"
            cp -r "$variant_dir"/* "$out/modules/$module_name/"
            echo "Installed $module_name from lgx into modules/$module_name/"
          else
            echo "Warning: could not read module name from lgx manifest" >&2
          fi
        else
          echo "Warning: no matching platform variant in lgx package" >&2
        fi
        rm -rf "$extract_dir"
      else
        echo "Warning: no .lgx file found for capability_module" >&2
      fi

      runHook postInstall
    '';

    meta = {
      description = "Generic standalone Qt shell for loading and testing Logos UI plugins";
      platforms = pkgs.lib.platforms.unix;
    };
  }
